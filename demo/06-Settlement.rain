

------------------------------------------------------------ Market Creation --
-- Proposal to create a new market by the market registry.
fact ProposeMarket
        [ market: Symbol, registry: Party, regulator: Party ]

-- Regular accepts the market proposal.
fact AcceptMarket
        [ market: Symbol, registry: Party, regulator: Party ]

-- New market containing a list of parties that can be invited
-- to issue securities on that market.
fact Market
        [ market: Symbol, registry: Party, regulator: Party
        , issuers: Set Party, invitedIssuers: Set Party ]

-- The registry proposes a new market, and the regulator accepts it.
rule makeMarket
await ProposeMarket [ market = ?m, registry = ?r, regulator = ?a ] gain {r}
  and AcceptMarket  [ market =  m, registry =  r, regulator =  a ] gain {a}
to
  say Market
        [ market = m, registry = r, regulator = a
        , issuers = {}, invitedIssuers = {} ]
   by  {r, a}
   use {'addIssuer, 'issueSecurity, 'bilateralDemandSettlement}


------------------------------------------------------------- Adding Issuers --
-- Invitation by the registry to add an issuer of securities to the market.
fact InviteIssuer [ market: Symbol, registry: Party, issuer: Party ]

-- Issuer accepts the invitation.
fact AcceptIssuer [ market: Symbol, issuer: Party ]

-- Addition of invited issuers to a market.
--   When the issuer is added to the market it also gains their authority.
--   In this version any new issuer can be added, but we could extend it
--   so only issuers listed in the given set can be added.
rule addIssuer
await Market
        [ market  = ?m, registry = ?r, regulator = ?a
        , issuers = ?is, invitedIssuers = ?iis ]
        gain {r, a}

  and InviteIssuer
        [ market = m, registry = r, issuer = ?i ]
        gain {r}

  and AcceptIssuer
        [ market = m, issuer = i ]
        where i ∉ is ∧ i ∉ iis
        gain {i}
to
  say Market
        [ market = m, registry = r, regulator = a
        , issuers = {i} ∪ is
        , invitedIssuers = iis ∖ {i} ]
   by  {r, a} obs {i} ∪ is
   use {'addIssuer, 'issueSecurity, 'bilateralDemandSettlement}


----------------------------------------------------------- Issue Securities --
-- Statement by an issuer to issue securities to a new holder.
fact IssueSecurity
        [ market: Symbol, holder: Party, issuer: Party
        , security: Text, amount: Nat ]

-- Holding record for some securities.
fact Holding
        [ market: Symbol, registry: Party, regulator: Party
        , holder: Party,  issuer: Party,   security: Text ]

-- Issue a new security on the market.
rule issueSecurity
await Market
        [ market  = ?m, registry = ?r, regulator = ?a
        , issuers = ?is ]
        consume none gain { r, a }

  and IssueSecurity
        [ market =  m, holder = ?h, issuer = ?i
        , security = ?s, amount = ?n]
        where i ∈ is gain {i, h}
to
  say Holding
        [ market = m, registry = r, regulator = a
        , holder = h, issuer   = i, security  = s ]
   by  {r, a, h, i}
   use {'bilateralDemandSettlement}
   num n


----------------------------------------------------------------- Settlement --
fact BilateralDemandSettlement
        [ market: Symbol
        , lhs: Party, lhsSecurity: Text, lhsAmount: Nat
        , rhs: Party, rhsSecurity: Text, rhsAmount: Nat ]

rule bilateralDemandSettlement
await Market
        [ market = ?m, registry = ?r, regulator = ?a, issuers = ?is ]
        consume none  gain {r, a}

  and BilateralDemandSettlement
        [ market = ?m
        , lhs = ?lp, lhsSecurity = ?ls, lhsAmount = ?la
        , rhs = ?rp, rhsSecurity = ?rs, rhsAmount = ?ra ]
        gain {lp}

  and BilateralDemandSettlement
        [ market = ?m
        , lhs = ?lp, lhsSecurity = ?ls, lhsAmount = ?la
        , rhs = ?rp, rhsSecurity = ?rs, rhsAmount = ?ra ]
        gain {rp}

  and Holding
        [ market = ?m,  registry = ?r, regulator = ?a
        , holder = ?lp, issuer  = ?li, security  = ?ls ]
        consume la gain {r, a, lp, li}

  and Holding
        [ market = ?m,  registry = ?r,  regulator = ?a
        , holder = ?rp, issuer   = ?ri, security  = ?rs ]
        consume ra gain {r, a, rp, ri}
to
  (say Holding
        [ market = m,  registry = r, regulator = a
        , holder = lp, issuer = ri,  security = rs ]
   by {r, a, lp, ri} use {'bilateralDemandSettlement} num ra)
 ∪+
  (say Holding
        [ market = m,  registry = r, regulator = a
        , holder = rp, issuer = li,  security = ls ]
   by {r, a, rp, li} use {'bilateralDemandSettlement} num la)


-------------------------------------------------------------------------------
scenario test do

 -- Rachel (registry) and Rodger (regulator) agree to create a new market.
 add    say ProposeMarket
         [ market = 'Mark, registry = !Rachel, regulator = !Rodger]
         by {!Rachel} obs {!Rodger} use {'makeMarket}

 add    say AcceptMarket
         [ market = 'Mark, registry = !Rachel, regulator = !Rodger]
         by {!Rodger} obs {!Rachel} use {'makeMarket}

 fire   by {!Rodger} rules {'makeMarket}
        dump

 -- Rachel (registry) invites Isabelle as a new issuer,
 -- and Isabelle accepts the invitation.
 add    say InviteIssuer
         [ market = 'Mark, registry = !Rachel, issuer = !Isabelle]
         by {!Rachel}   obs {!Isabelle} use {'addIssuer}

 add    say AcceptIssuer
         [ market = 'Mark, issuer = !Isabelle]
         by {!Isabelle} obs {!Rachel} use {'addIssuer}

 fire   by {!Rachel} rules {'addIssuer}
        dump

 -- Isabelle issues Plankton Investment shares to Horace the holder,
 --  and some Clotted Clams to Harold the holder.
 add    say IssueSecurity
         [ market = 'Mark, holder = !Horace, issuer = !Isabelle
         , security = "Plankton Investments"
         , amount = 100 ]
         by {!Isabelle, !Horace}
         use {'issueSecurity}

 fire   by {!Isabelle} rules {'issueSecurity}

 add    say IssueSecurity
         [ market = 'Mark, holder = !Harold, issuer = !Isabelle
         , security = "Clotted Clams"
         , amount = 100 ]
         by {!Isabelle, !Harold}
         use {'issueSecurity}

 fire   by {!Isabelle} rules {'issueSecurity}

        dump

-- Horace and Harold swap their respective shares.
add    say BilateralDemandSettlement
        [ market = 'Mark
        , lhs = !Horace, lhsSecurity = "Plankton Investments", lhsAmount = 100
        , rhs = !Harold, rhsSecurity = "Clotted Clams",        rhsAmount = 100 ]
        by {!Horace} obs {!Rachel} use {'bilateralDemandSettlement}

add    say BilateralDemandSettlement
        [ market = 'Mark
        , lhs = !Horace, lhsSecurity = "Plankton Investments", lhsAmount = 100
        , rhs = !Harold, rhsSecurity = "Clotted Clams",        rhsAmount = 100 ]
        by {!Harold} obs {!Rachel} use {'bilateralDemandSettlement}

fire    by {!Rachel} rules {'bilateralDemandSettlement}
        dump